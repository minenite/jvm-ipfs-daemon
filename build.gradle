
plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.71'
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'

group 'fr.rhaz.ipfs'
version '1.0.7'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.apache.commons:commons-lang3:3.7"
    compile "org.apache.commons:commons-compress:1.5"
}

jar {
    manifest { attributes('Main-Class': 'fr.rhaz.ipfs.IPFSDaemonKt') }
    destinationDir = file("$rootDir/jar")
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

Properties props = new Properties();
props.load(new FileInputStream("properties"))
publishing {
    repositories {
        maven {
            url props.getProperty("mavenwrite")
        }
    }

    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact packageJavadoc
        }
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath

    options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor true

        links "https://docs.oracle.com/javase/8/docs/api/"
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task packageJavadoc(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

processResources {
    with copySpec { from 'resources' }
}

sourceSets {
    main.kotlin.srcDirs += 'src'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}